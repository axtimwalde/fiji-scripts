/**
 * This script takes two related TrakEM2 projects on the same image data as
 * input.  All patches in the first project will be set to the min and max of
 * their counterpart in project2.  This result is applied to the first project.
 */

import ij.*;
import ij.gui.*;
 
import ini.trakem2.display.*;

import java.util.HashMap;
import java.util.ArrayList;
import java.util.Set;

project1 = null;
project2 = null;

boolean setup()
{
	gd = new GenericDialog( "Deform Montages to Multi-Layer-Mosaic" );
	
	projects = Project.getProjects();		
	if ( projects.size() < 2 )
		return false;
	
	projectNames = new String[ projects.size() ];
	for ( i = 0; i < projects.size(); ++i )
		projectNames[ i ] = projects.get( i ).toString();
	
	gd.addChoice( "independent-montages :", projectNames, projectNames[ 0 ] );
	gd.addChoice( "multi-layer-montages :", projectNames, projectNames[ 1 ] );
		
	gd.showDialog();
	if ( gd.wasCanceled() ) return false;
	
	project1 = projects.get( gd.getNextChoiceIndex() );
	project2 = projects.get( gd.getNextChoiceIndex() );
	
	return true;
}

float[] patchCenterToWorld( patch )
{
	c = new float[]{ ( float )patch.getOWidth() / 2.0f,( float )patch.getOHeight() / 2.0f };
	ct = patch.getCoordinateTransform();
	at = patch.getAffineTransformCopy();
	if ( ct != null )
	{
		box = patch.getCoordinateTransformBoundingBox();
		at.translate( -box.x, -box.y );
		ct.applyInPlace( c );
	}
	affine = new AffineModel2D();
	affine.set( at );
	affine.applyInPlace( c );
	return c;
}

void run()
{
	if ( !setup() ) return;
	
	IJ.log( "deforming..." );
		
	/* create one deformation control point at the center point of each tile */
	/* HashMap< Long, Point > */
	cp = new HashMap();

	IJ.log( "Collecting source points..." );
	/* all patches from the montage project */
	patches = project1.getRootLayerSet().getDisplayables( Patch.class );
	i = 0;
	for ( patch : patches )
	{
		float[] c = patchCenterToWorld( patch );
		Point p = new Point( c );
		cp.put( patch.getId(), new PointMatch( p, new Point( new float[ 2 ] ) ) );
		IJ.showProgress( ++i, patches.size() );
	}

	IJ.log( "Collecting target points..." );
	patches = project2.getRootLayerSet().getDisplayables( Patch.class );
	i = 0;
	for ( patch : patches )
	{
		if ( cp.containsKey( patch.getId() ) )
		{
			float[] c = patchCenterToWorld( patch );
			Point q = cp.get( patch.getId() ).getP2();
			float[] w = q.getW();
			w[ 0 ] = c[ 0 ];
			w[ 1 ] = c[ 1 ];
		}
		IJ.showProgress( ++i, patches.size() );
	}
	
	
	for ( layer : project1.getRootLayerSet().getLayers() )
	{
		IJ.log( "Processing layer " + layer );
				
		matches = new ArrayList();
		
		for ( patch : layer.getDisplayables( Patch.class ) )
			matches.add( cp.get( patch.getId() ) );
		
		for ( patch : layer.getDisplayables( Patch.class ) )
		{
			try
			{
				box = patch.getCoordinateTransformBoundingBox();
				at = new AffineTransform();
				at.translate( -box.x, -box.y );
				at.preConcatenate( patch.getAffineTransform() );
							
				mpicbg.trakem2.transform.AffineModel2D toWorld = new mpicbg.trakem2.transform.AffineModel2D();
				toWorld.set( at );
							
				mlst = Align.createMLST( matches, 1.0f );
							
				ctl = new CoordinateTransformList();
				ctl.add( toWorld );
				ctl.add( mlst );
				ctl.add( toWorld.createInverse() );
				
				patch.appendCoordinateTransform( ctl );
				
				patch.updateMipMaps();
			}
			catch ( Exception e )
			{
				e.printStackTrace();
			}
		}
	}
}

run();

